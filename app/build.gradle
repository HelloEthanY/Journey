apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [orientation: rootProject.ext.android.screenOrientation]
        // landscape : 横屏     portrait ：竖屏
        multiDexEnabled true // 使用分包
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            //是否自动清理未使用资源
            shrinkResources true
        }
    }

    // 自定义输出配置 3.0的写法
    applicationVariants.all { variant ->
        variant.outputs.all {
            // 输出apk名称为【包名_渠道名_版本号_版本名_日期】
            def fileName = "Journey_build_${defaultConfig.versionCode}_v${defaultConfig.versionName}_${buildTime()}.apk"
            outputFileName = fileName
        }
    }

    dataBinding {
        enabled true
    }

    sourceSets {
        main {
            jniLibs.srcDir 'libs'
        }
    }
    // 添加图表需要新增的
    /*   packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }*/
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:design:27.1.1'
    // cardView
    implementation 'com.android.support:cardview-v7:27.1.1'
    // 底部Tab
    implementation 'me.majiajie:pager-bottom-tab-strip:2.2.5'
    // 下拉刷新
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-14'
    // mvvmhabit
    implementation project(path: ':mvvmhabit')
    // 百度地图
    implementation files('libs/BaiduLBS_Android.jar')
    implementation files('libs/component_common_sdk_1.1.0.jar')
    implementation files('libs/IndoorscapeAlbumPlugin.jar')
    //multidex分包
    implementation 'com.android.support:multidex:1.0.3'
    // 城市选择
    implementation project(':citypicker')
    // 定制的ijkplayer
    implementation project(path: ':jjdxm-ijkplayer')
    implementation rootProject.ext.jjdxm_v7
    //图表框架
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    // implementation project(':MPChartLib')
    implementation project(':skyline')
    // 混合开发框架
    implementation 'com.github.lzyzsd:jsbridge:1.0.4'
}
//获取当前时间
def static buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    formattedDate
}
